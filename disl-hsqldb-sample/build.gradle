/*
 * Copyright 2015 Karel Hübl <karel.huebl@gmail.com>.
 *
 * This file is part of disl.
 *
 * Disl is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Disl is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
 */

 plugins {
	 id "com.github.hierynomus.license" version "0.11.0"
 }
 
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

mainClassName='org.disl.workflow.DislLauncher'

repositories {
	mavenLocal()
	mavenCentral()
	//GEM System a.s. local repository,
	//  set gemMavenUserRead and gemMavenPasswordRead in ~/.gradle/gradle.properties
	//  Email sales@gemsystem.cz for credentials or build disl into your local maven repository. Use command in disl project: "gradle install"
	maven {
		credentials {
			username gemMavenUserRead
			password gemMavenPasswordRead
		}
		url "https://maven.gemsystem.cz/nexus/content/repositories/public/"
	}
}

dependencies {
	compile 'com.github.gemsystem:disl:1.2.2-SNAPSHOT'
    testCompile 'junit:junit:4.12'
	runtime 'org.hsqldb:hsqldb:2.3.2'
	runtime 'org.slf4j:slf4j-simple:1.7.21'
}

license { 
	header rootProject.file('HEADER.txt')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	skipExistingHeaders = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}
 
artifacts {
	archives sourcesJar
}

task generateWiki (type:JavaExec) {
	main = 'generateWiki'
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs '-Dwiki.root.dir=build/wiki'
}

task cleanWiki(type: Delete) {
	delete 'build/wiki'
}

task syncWikiSource(type: Sync,dependsOn: 'cleanWiki') {
	from 'wiki'
	into 'build/wiki'
}

task copyGeneratedFiles(type: Copy, dependsOn: 'generateWiki') {
	from 'disl-hsqldb-sample/build/wiki'
	into 'build/wiki'
}

task hugo(type:Exec) {
	workingDir = 'build/wiki'
	commandLine 'hugo'

	dependsOn syncWikiSource
	dependsOn copyGeneratedFiles
	tasks.findByName('copyGeneratedFiles').mustRunAfter 'syncWikiSource'
}

task deployWiki(type: Sync, dependsOn: hugo) {
	from '/build/wiki/public'
	into 'C:\\Apache\\htdocs'
}